/*
 * Copyright 2021 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.github.johnrengelman.shadow' version '6.1.0'
}


description = 'Message Format Library (classpath scanner)'


def asmVersion = "9.1"


dependencies {
  api project(':message-format')

  implementation "org.ow2.asm:asm:${asmVersion}"
}


shadowJar {
  group 'build'

  archiveClassifier.set(null)  // set by shadowJar but interferes with publishing

  dependencies {
    exclude project(':message-format')
    exclude(dependency {
      it.moduleGroup == 'org.antlr'
    })
  }

  exclude 'META-INF/maven/**'
  exclude 'module-info.class'

  minimize()

  relocate 'org.objectweb.asm', 'de.sayayi.lib.message.scanner.asm'

  manifest {
    attributes['Implementation-Title'] = "${project.description} (no dependencies)"
  }
}


jar.enabled = false


assemble.dependsOn shadowJar


publishing {
  publications {

    cpscan(MavenPublication) { publication ->
      from project.shadow.component(publication)

      pom {
        name = "${project.description}"
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2021'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.cpscan
}
