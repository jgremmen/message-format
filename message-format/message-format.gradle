/*
 * Copyright 2020 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//file:noinspection DependencyNotationArgument

plugins {
  id 'antlr'
  id 'idea'
  id 'de.sayayi.plugin.gradle.libapi' version '1.0-SNAPSHOT'
}


description = 'Message Format Library'


def antlrVersion = '[4.13.0,4.14)'
def antlrExtVersion = '[0.5,0.6)'
def asmVersion = '[9.0,10.0)'
def caffeineJCacheVersion = '3.1.8'
def javaxCacheApiVersion = '1.1.1'


configurations {
  antlr4 {
    visible = false
  }

  api.setExtendsFrom([])

  configureEach {
    resolutionStrategy.cacheDynamicVersionsFor 15, 'minutes'
  }
}


java {
  registerFeature('optional') {
    usingSourceSet sourceSets.main
  }
}


dependencies {
  // main
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"

  implementation("de.sayayi.lib:antlr4-runtime-ext:${antlrExtVersion}") {
    exclude group: 'org.jetbrains'
    exclude group: 'org.antlr'
  }
  implementation "org.antlr:antlr4-runtime:${antlrVersion}"

  optionalImplementation "org.ow2.asm:asm:${asmVersion}"

  // test
  testImplementation gradleTestKit()
  testImplementation "com.github.ben-manes.caffeine:jcache:${caffeineJCacheVersion}"

  antlr4 "org.antlr:antlr4:${antlrVersion}"
}


def grammarOutputDirectory = layout.buildDirectory
    .dir('generated-src/antlr/main/de/sayayi/lib/message/internal/parser').get().asFile

generateGrammarSource {
  antlrClasspath = configurations.antlr4
  outputDirectory = grammarOutputDirectory

  arguments += [ '-package', 'de.sayayi.lib.message.internal.parser' ]
}


compileJava.dependsOn generateGrammarSource


sourcesJar {
  dependsOn generateGrammarSource

  exclude 'gen/*'
  exclude '**/*.interp'
}


jar {
  manifest {
    attributes['Implementation-Title'] = project.description
  }
}


def resolvedAntlrVersion = configurations
    .compileClasspath
    .resolvedConfiguration
    .resolvedArtifacts
    .find { it.name == 'antlr4-runtime' }
    .moduleVersion.id.version

def resolvedAntlrExtVersion = configurations
    .compileClasspath
    .resolvedConfiguration
    .resolvedArtifacts
    .find { it.name == 'antlr4-runtime-ext' }
    .moduleVersion.id.version


javadoc {
  (options as StandardJavadocDocletOptions).with {
    linksOffline(
        "https://javadoc.io/doc/org.antlr/antlr4-runtime/${resolvedAntlrVersion}/",
        "${rootDir}/gradle/javadocs/antlr4-runtime")
    linksOffline(
        "https://javadoc.io/doc/de.sayayi.lib/antlr4-runtime-ext/${resolvedAntlrExtVersion}/",
        "${rootDir}/gradle/javadocs/antlr4-runtime-ext")
    linksOffline(
        "https://javadoc.io/doc/javax.cache/cache-api/${javaxCacheApiVersion}/",
        "${rootDir}/gradle/javadocs/javax-cache")
  }

  exclude '**/parser/MessageLexer.java'
  exclude '**/parser/MessageParser*.java'
  exclude '**/pack/*.java'
  exclude '**/*Impl.java'
}


publishing {
  publications {

    msgformat(MavenPublication) {
      suppressPomMetadataWarningsFor 'optionalApiElements'
      suppressPomMetadataWarningsFor 'optionalRuntimeElements'

      from components.java

      pom {
        name = 'Java Message Format Library'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.msgformat
}


clean.doLast {
  fileTree("${projectDir}/src/main").matching {
    include 'antlr/*.tokens'
  }.files.forEach({ it.delete() })

  file("${projectDir}/src/main/antlr/gen").deleteDir()
  file("${projectDir}/src/main/gen").deleteDir()
}


idea {
  module {
    resourceDirs += file('src/main/antlr')
  }
}
