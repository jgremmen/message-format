/*
 * Copyright 2020 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//file:noinspection DependencyNotationArgument

plugins {
  id 'antlr'
  id 'idea'
  id 'de.sayayi.plugin.gradle.libapi' version "1.0-SNAPSHOT"
}


description = 'Message Format Library'


def antlrVersion = "4.9.3"
def antlrExtVersion = "0.3.1"
def asmVersion = "[6.0,)"
def ehcacheVersion = "3.10.+"
def javaxCacheApiVersion = "1.1.1"
def springCoreVersion = "5.3.29"


configurations.configureEach {
  resolutionStrategy.cacheDynamicVersionsFor 30, 'minutes'
}


java {
  registerFeature('optional') {
    usingSourceSet sourceSets.main
  }
}


dependencies {
  // main
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"

  implementation("de.sayayi.lib:antlr4-runtime-ext") {
    version {
      strictly '[0.3,0.4)'
      prefer antlrExtVersion
    }

    exclude group: 'org.antlr', module: 'antlr4'
    exclude group: 'org.jetbrains'
  }

  optionalImplementation "javax.cache:cache-api:${javaxCacheApiVersion}"
  optionalImplementation "org.ow2.asm:asm:${asmVersion}"
  optionalImplementation("org.springframework:spring-core") {
    version {
      strictly '[5.0,6.0)'
      prefer springCoreVersion
    }

    transitive = false
  }

  // test
  testImplementation("org.ehcache:ehcache:${ehcacheVersion}") {
    exclude module: "jaxb-runtime"
  }

  antlr "org.antlr:antlr4:${antlrVersion}"
}


configurations {
  testAnnotationProcessor.extendsFrom annotationProcessor
  testCompileOnly.extendsFrom compileOnly
}


generateGrammarSource {
  outputDirectory = file("${buildDir}/generated-src/antlr/main/de/sayayi/lib/message/parser")
  arguments += [ '-package', 'de.sayayi.lib.message.parser' ]
}


compileJava.dependsOn generateGrammarSource


sourcesJar {
  dependsOn generateGrammarSource

  exclude "gen/*"
  exclude "**/*.interp"
}


jar {
  manifest {
    attributes['Implementation-Title'] = "${project.description}"
  }
}


javadoc {
  (options as StandardJavadocDocletOptions).with {
    linksOffline "https://javadoc.io/doc/org.antlr/antlr4-runtime/${antlrVersion}/",
        "${rootDir}/gradle/javadocs/antlr4-runtime"
    linksOffline "https://javadoc.io/doc/de.sayayi.lib/antlr4-runtime-ext/${antlrExtVersion}/",
        "${rootDir}/gradle/javadocs/antlr4-runtime-ext"
    linksOffline "https://javadoc.io/doc/org.springframework/spring-core/${springCoreVersion}/",
        "${rootDir}/gradle/javadocs/spring-core"
  }

  exclude '**/parser/MessageLexer.java'
  exclude '**/parser/MessageParser*.java'
  exclude '**/pack/*.java'
  exclude '**/*Impl.java'
}


publishing {
  publications {

    msgformat(MavenPublication) {
      suppressPomMetadataWarningsFor 'optionalApiElements'
      suppressPomMetadataWarningsFor 'optionalRuntimeElements'

      from components.java

      pom {
        name = 'Java Message Format Library'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.msgformat
}


clean.doLast {
  fileTree("${projectDir}/src/main").matching {
    include "antlr/*.tokens"
  }.files.forEach({ it.delete() })
  file("${projectDir}/src/main/gen").delete()
}


idea {
  module {
    resourceDirs += file("src/main/antlr")
  }
}
