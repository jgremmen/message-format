/*
 * Copyright 2020 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'antlr'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '6.1.0'
  id 'nebula.optional-base' version '5.0.3'
}


description = 'Message Format Library'


configurations {
  compile {
    extendsFrom = extendsFrom.findAll { it != configurations.antlr }
  }
}


def antlrVersion = "4.9.2"
def ehcacheVersion = "3.9.7"
def javaxCacheApiVersion = "1.1.1"


dependencies {
  implementation "org.antlr:antlr4-runtime:${antlrVersion}"

  implementation "javax.cache:cache-api:${javaxCacheApiVersion}", optional

  testImplementation "org.ehcache:ehcache:${ehcacheVersion}"

  antlr "org.antlr:antlr4:${antlrVersion}"
}


generateGrammarSource {
  outputDirectory = file("${buildDir}/generated-src/antlr/main/de/sayayi/lib/message/parser")
  arguments += [ '-package', 'de.sayayi.lib.message.parser' ]
}


compileJava.dependsOn generateGrammarSource


sourcesJar {
  dependsOn generateGrammarSource

  exclude "**/*.tokens"
  exclude "**/*.g4"
}


jar {
  manifest {
    attributes['Implementation-Title'] = "${project.description}"
  }
}


task javadocShadowJar(type: Jar, group: 'build') {
  dependsOn javadocJar

  archiveAppendix.set('nodep')
  archiveClassifier.set('javadoc')

  with javadocJar

  duplicatesStrategy DuplicatesStrategy.EXCLUDE
}


task sourcesShadowJar(type: Jar, group: 'build') {
  dependsOn sourcesJar

  archiveAppendix.set('nodep')
  archiveClassifier.set('sources')

  with sourcesJar

  duplicatesStrategy DuplicatesStrategy.EXCLUDE
}


shadowJar {
  group 'build'

  dependsOn sourcesShadowJar, javadocShadowJar

  archiveAppendix.set('nodep')
  archiveClassifier.set(null)  // set by shadowJar but interferes with publishing

  dependencies {
    exclude(dependency('com.ibm.icu::'))
    exclude(dependency('javax.cache::'))
    exclude(dependency('org.glassfish::'))
  }

  exclude 'META-INF/maven/**'
  exclude "org/antlr/v4/runtime/atn/ATNSerializer*"
  exclude "org/antlr/v4/runtime/ParserInterpreter*"
  exclude "org/antlr/v4/runtime/TokenStreamRewriter*"
  exclude "org/antlr/v4/runtime/tree/pattern/**"
  exclude "org/antlr/v4/runtime/tree/Trees"
  exclude "org/antlr/v4/runtime/tree/xpath/**"
  exclude "org/antlr/v4/runtime/Unbuffered*"
  exclude "org/antlr/v4/tool/**"

  minimize()

  relocate 'org.antlr.v4.runtime', 'de.sayayi.lib.message.antlr'

  manifest {
    attributes['Implementation-Title'] = "${project.description} (no dependencies)"
  }
}


import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task testShadowJar(type: ShadowJar) {
  dependsOn compileTestJava

  archiveAppendix.set('test')

  from sourceSets.test.output
  configurations = [project.configurations.testRuntimeClasspath]

  relocate 'org.antlr.v4.runtime', 'de.sayayi.lib.message.antlr'
}


task shadowTest(type: Test, group: 'verification') {
  dependsOn shadowJar, testShadowJar

  doFirst {
    classpath = shadowJar.outputs.files + testShadowJar.outputs.files + classpath -
        compileJava.outputs.files - compileTestJava.outputs.files
  }
}


assemble.dependsOn shadowJar
build.dependsOn shadowTest


publishing {
  publications {

    dep(MavenPublication) {
      artifact jar
      artifact javadocJar
      artifact sourcesJar

      versionMapping {
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'Java Message Format Library'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }

        withXml {
          def dependenciesNode = asNode().appendNode('dependencies')

          configurations.implementation.allDependencies.each {
            if (it.name == 'antlr4-runtime')
            {
              def dependencyNode = dependenciesNode.appendNode('dependency')
              dependencyNode.appendNode('groupId', it.group)
              dependencyNode.appendNode('artifactId', it.name)
              dependencyNode.appendNode('version', it.version)
            }
          }
        }
      }
    }


    noDep(MavenPublication) { publication ->
      artifactId 'message-format-nodep'

      alias = true

      from project.shadow.component(publication)
      artifact javadocShadowJar
      artifact sourcesShadowJar

      pom {
        name = 'Java Message Format Library (no dependencies)'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.dep
  sign publishing.publications.noDep
}


clean.doLast {
  fileTree("${projectDir}/src/main").matching {
    include "antlr/*.tokens"
  }.files.forEach({ it.delete() })
  file("${projectDir}/src/main/gen").delete()
}


idea {
  module {
    resourceDirs += file("src/main/antlr")
  }
}
