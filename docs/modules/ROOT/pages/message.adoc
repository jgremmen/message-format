= Message
:navtitle: Message

This chapter describes the message format - or better said - it's syntactical structure in more detail.

Essentially, a message is a description for generating human readable text. Nobody wants to present the user
with a message like `the search found 1 results`. Yet, generated messages often lack grammatical consistency.
This is especially true for some languages where the grammar is more complex.

An message could read `I give the man/woman a book`. So man and woman are exchangeable and the sentence is
still grammatically correct. For a language like german, it would be `Ich gebe dem Mann/der Frau ein Buch`.
Here `the man` translates to `dem Mann` and `the woman` translates to `der Frau`.

Let's assume the parameter to format is a character (`M` = man, `F` = woman) which represents the gender.
The messages for the english, german and italian language would be:

[cols="1,6"]
|===
|Language|Message

|english
|`"I give the %{gender,choice,'M':man,'F':woman} a book"`

|german
|`"Ich gebe %{gender,choice,'M':'dem Mann','F':'der Frau'} ein Buch"`

|italian
|`"Do %{gender,choice,'M':'all\u0027uomo un libro','F':'un libro alla donna'}"`
|===

Using an appropriate `MessageBundle` message formats can be defined for each language the application has to
be able to present. Just provide the message code, locale and parameters and let the message format library
do the formatting.




[[message-syntax]]
== Message Syntax

A message is an alternating sequence of text and parameter parts. The parameter parts are placeholders for
parameter values which are provided during runtime and rendered based on the value type, optional format
designator and parameter configuration. The remaining text parts are formatted as-is with the exception of
escaped characters.

[plantuml,message,svg,align=left]
....
@startebnf
message = [text], { parameter, [text] };
@endebnf
....

Text parts are merely a chain of visible/printable characters and escape sequences.
The parameter parts on the other hand have a more complex structure, which is discussed in the
xref:message.adoc#parameter-syntax[parameter syntax] chapter.

[plantuml,text,svg,align=left]
....
@startebnf
text = { character }-;

character = ('\u', hex-digit, hex-digit, hex-digit, hex-digit | '\', ('"' | "'" | '%' | '{' | '\') | ? \u0020-\uffff ? (* visible characters only (no control characters) *) );

hex-digit = (? 0-9 ? | ? a-f or A-F ?);
@endebnf
....

Let's take a look at some text part examples:
[cols="3,2"]
|===
|Message|Possible output

|`The&nbsp;&nbsp;&nbsp;quick brown&nbsp;&nbsp;fox`
|`The quick brown fox`

|`temperature = 10\u00b0C`
|`temperature = 10Â°C`

|`/\\`
|`/\`

|`&nbsp;whitespace&nbsp;`
|`whitespace` footnote:[
    leading and trailing spaces are preserved during formatting and removed from the final output. For more detail
    see the chapter on xref:message.adoc#whitespace-handling[whitespace handling].]

|`I see a %\{color} ball.`
|`I see a red ball.`

|`I ate %{n,choice,1:cookie,:'%\{n} cookies'}`
|`I ate 12 cookies`

|`The numbers are: %{numbers,list-sep-last:' and '}`
|`The numbers are: 2, 56, 7 and 99`

|`\q`
|`\q`
|===




[[parameter-syntax]]
== Parameter Syntax

The parameter part describes which parameter value to format and how. In it's simplest form it will only specify
the parameter name but it's formatting can be controlled in more depth by specifying a named formatter and/or
configuring the formatter.

[plantuml,parameter,svg,align=left]
....
@startebnf
parameter = '%{', name, [',', format], {',', config}, [',', ':', quoted-message], '}';
(* name / format = name or keyword (true, false, null, empty) *)
@endebnf
....

Parameter name::
    When formatting a message, a parameter map is provided. The key represents the parameter name, the value is any
    possible object or `null`. So the name in the parameter part specifies which value from the map must be formatted.

[plantuml,name,svg,align=left]
....
@startebnf
name = ( ? a-z ? | ? A-Z ? ), { ? a-z ? | ? A-Z ? | ? 0-9 ? | '_' };
@endebnf
....

Parameter format::
    This is an optional setting and forces a specific named formatter to be used when formatting the value. If no
    `format` is specified or if the value cannot be formatted by the specified named formatter, a suitable formatter
    is selected from the `FormatterService`.

Parameter config::
    The optional configuration section provides the formatter with additional configuration settings. It is a comma
    separated sequence of key-value pairs. The configuration keys are specific for each formatter. See the
    xref:formatters.adoc[formatters] chapter for more details.

[plantuml,config,svg,align=left]
....
@startebnf
config = config-key, ':', config-value;
@endebnf
....

[plantuml,config-key,svg,align=left]
....
@startebnf
config-key = (bool | name | [('=' | '!' | '<>' | '<' | '<=' | '>' | '>=')], (string | number) | ('=' | '!' | '<>'), ('null' | 'empty'));
@endebnf
....

Configuration key bool::
    Boolean keys are used for formatting boolean values. They can be used to translate a boolean value in any
    corresponding string representation. +
    Eg. `%{b,true:yes,false:'no!'}`

[plantuml,bool,svg,align=left]
....
@startebnf
bool = ('true' | 'false');
@endebnf
....

Configuration key name::
    Key names are exclusively used to provide the formatter with configuration settings. +
    Eg. `%{list,list-sep:' , ',list-sep-last:' and '}`

Configuration key null::
    Key `null` is a generic key and it's value will be used in most cases where the parameter value is `null`. +
    Eg. `%{v,null:"<not specified>"}`

Configuration key empty::
    Key `empty` is a generic key and it's value will be used in most cases where the parameter value is considered
    empty. This may match an empty string or an empty collection. +
    Eg. `%{list,empty:'no elements'}`

Configuration key string and number::
    These keys are used by certain formatters (eg. xref:formatters_named.adoc#choice[`choice`] formatter) as way of translating the parameter value into
    a string representation. +
    Eg. `%{n,choice,0:'no cookies',1:'a cookie',:'%\{n} cookies'}`

[plantuml,string,svg,align=left]
....
@startebnf
string = ('"', [text], '"' | "'", [text], "'");
@endebnf
....

[plantuml,number,svg,align=left]
....
@startebnf
number = ['-'], { ? 0-9 ?}-;
@endebnf
....

Configuration key operation::
    An operation changes the behavior on how the configuration key is matched.
+
--
[cols="1,5"]
|===
|Operation|Description

|`=`
|Check for equality. This is the default if no operation is specified. +
 Eg. `%{n,=1:'exactly one'}`

|`!` or `<>`
|Check for inequality. +
 Eg. `"Error %{msg,!empty:': %\{msg}}"`

|`>`
|Check whether the parameter value is larger than the configuration key. +
 Eg. `%{s,>'AA':'larger than AA'}`

|`>=`
|Check whether the parameter value is larger than or equal to the configuration key. +
 Eg. `%{n,>=10:'at least 2 digits'}`

|`<`
|Check whether the parameter value is less than the configuration key. +
 Eg. `%{n,<0:negative,>0:positive}`

|`\<=`
|Check whether the parameter value is less than or equal to the configuration key. +
Eg. `%{s,\<='ZZ':'less than or equal to ZZ'}`
|===
--

Configuration value::
    There are 4 configuration value types (bool, number, string, message). Keywords are considered string values and
    provide a way to specify strings without quotes.

[plantuml,config-value,svg,align=left]
....
@startebnf
config-value = (bool | number | quoted-message | string | name | 'null' | 'empty');
(* name, null and empty represent a string value (without quotes) *)
@endebnf
....

Configuration value quoted-message::
    A quoted message is a message as described in the xref::message.adoc#message-syntax[message syntax] chapter.
    It can contain message parameter parts which allows for complex message formatting. +
    Eg. `"An error has occurred%{ex,!empty: #': %\{ex}'#}"`

[plantuml,quoted-message,svg,align=left]
....
@startebnf
quoted-message = ('"', [message], '"' | "'", [message], "'");
@endebnf
....

[[whitespace-handling]]
== Whitespace Handling

Multiple consecutive whitespace characters are collapsed into 1 single whitespace.
