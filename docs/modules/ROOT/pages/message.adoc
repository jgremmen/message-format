= Message
:navtitle: Message

[[message-syntax]]
== Message Syntax

A message is an alternating sequence of text and parameter parts. The parameter parts are placeholders for
parameter values which are provided during runtime. The remaining text parts are formatted
as-is with the exception of escaped characters.

[plantuml,message,svg,align=left]
....
@startebnf
message = [text], { parameter, [text] };
@endebnf
....

Text parts are just a chain of characters. The parameter parts on the other hand have a more complex structure,
which is discussed in the xref:message.adoc#parameter-syntax[parameter syntax] chapter.

[plantuml,text,svg,align=left]
....
@startebnf
text = { character }-;
@endebnf
....

A character is either a single regular character or an escape sequence.

[plantuml,character,svg,align=left]
....
@startebnf
character = ('\u', hex-digit, hex-digit, hex-digit, hex-digit | '\', ('"' | "'" | '%' | '{' | '\') | ? \u0020-\uffff ? (* visible characters only (no control characters) *) );
@endebnf
....

[plantuml,hex-digit,svg,align=left]
....
@startebnf
hex-digit = (? 0-9 ? | ? a-f or A-F ?);
@endebnf
....

Let's take a look at some examples:
[cols="1,1",width=40%]
|===
|Escape|Output

|`\{`
|`{`

|`\"`
|`"`

|`\u0021`
|`!`

|`\u003f`
|`?`

|`\q`
|`\q`
|===




[[parameter-syntax]]
== Parameter Syntax

[plantuml,parameter,svg,align=left]
....
@startebnf
parameter = '%{', name, [',', format], {',', config}, [',', ':', quoted-message], '}';
(* name / format = name or keyword (true, false, null, empty) *)
@endebnf
....

[plantuml,name,svg,align=left]
....
@startebnf
name = ( ? a-z ? | ? A-Z ? ), { ? a-z ? | ? A-Z ? | ? 0-9 ? | '_' };
@endebnf
....

[plantuml,bool,svg,align=left]
....
@startebnf
bool = ('true' | 'false');
@endebnf
....

[plantuml,config,svg,align=left]
....
@startebnf
config = config-key, ':', config-value;
@endebnf
....

[plantuml,config-key,svg,align=left]
....
@startebnf
config-key = (bool | name | [('=' | '!' | '<>' | '<' | '<=' | '>' | '>=')], (string | number) | ('=' | '!' | '<>'), ('null' | 'empty'));
@endebnf
....

[plantuml,string,svg,align=left]
....
@startebnf
string = ('"', [text], '"' | "'", [text], "'");
@endebnf
....

[plantuml,number,svg,align=left]
....
@startebnf
number = ['-'], { ? 0-9 ?}-;
@endebnf
....

[[whitespace-handling]]
== Whitespace Handling

Multiple consecutive whitespace characters are collapsed into 1 single whitespace.
