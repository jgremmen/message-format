/*
 * Copyright 2019 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


tasks.withType(Jar) {
  from files('LICENSE')

  reproducibleFileOrder true
}


task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set('sources')

  from project(':core').sourceSets.main.allSource
  from project('formatters-jdk8').sourceSets.main.allSource
  from sourceSets.main.allSource

  exclude "**/*.tokens"
}


artifacts {
  archives javadocJar
  archives sourcesJar
}


publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact javadocJar
      artifact sourcesJar

      versionMapping {
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'Java Message Format Library'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }

  repositories {
    maven {
      // maven central
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username = project.findProperty('mavenCentral.username')
        password = project.findProperty('mavenCentral.password')
      }
    }
  }
}


signing {
  sign publishing.publications.maven
}
