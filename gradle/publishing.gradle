/*
 * Copyright 2019 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


publishing {
  publications {

    dep(MavenPublication) {
      from components.java

      versionMapping {
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'Java Message Format Library'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }

        withXml {
          def dependenciesNode = asNode().dependencies[0]

          dependenciesNode.'*'.findAll().each() {
            it.parent().remove(it)
          }

          def dependency = dependenciesNode.appendNode('dependency')
          dependency.appendNode('groupId', 'org.antlr')
          dependency.appendNode('artifactId', 'antlr4-runtime')
          dependency.appendNode('version', "${antlrVersion}")
          dependency.appendNode('scope', 'runtime')
        }
      }
    }


    noDep(MavenPublication) { publication ->
      artifactId 'message-format-nodep'

      from project.shadow.component(publication)
      artifact javadocJar
      artifact sourcesJar

      versionMapping {
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'Java Message Format Library (no dependencies)'
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2019'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }

        withXml {
          def dependenciesNode = asNode().dependencies[0]
          dependenciesNode.parent().remove(dependenciesNode)
        }
      }
    }
  }

  repositories {
    maven {
      name = "mavenTemp"
      url = "$buildDir/repo"
    }
    maven {
      name = "mavenCentral"
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username = project.findProperty('mavenCentral.username')
        password = project.findProperty('mavenCentral.password')
      }
    }
  }
}


signing {
  sign publishing.publications.dep
  sign publishing.publications.noDep
}
