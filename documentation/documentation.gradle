/*
 * Copyright 2023 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.asciidoctor.jvm.convert' version '3.3.2'
}


description = 'Message Format Library Documentation'


repositories {
  mavenCentral()
}


asciidoctor {
  baseDirFollowsSourceFile()

  sources {
    include 'index.adoc'
    include 'release-notes.adoc'
    include 'migration-guide.adoc'
    include 'reference-guide.adoc'
  }

  attributes \
    backend: 'xhtml5',
    icons: 'font',
    fullVersion: rootProject.version,
    'source-highlighter': 'coderay',
    contextUrl: 'message-format/'

  options logDocuments: true
}


tasks.register('websiteZip', Zip) {
  description 'Creates a documentation website archive'
  group 'build'

  archiveFileName = "document-${project.version}.zip"
  destinationDirectory = layout.buildDirectory.dir('archives')

  reproducibleFileOrder = true
  duplicatesStrategy = DuplicatesStrategy.FAIL

  from(asciidoctor) {
    include 'reference-guide.html'
    into project.version
  }

  from(asciidoctor) {
    exclude 'reference-guide.html'
  }

  from(project(':message-format').javadoc) {
    into project.version + "/javadoc"
  }

  from(project(':message-format-cpscan').javadoc) {
    into project.version + "/javadoc-cpscan"
  }

  from(project(':message-format-jodatime').javadoc) {
    into project.version + "/javadoc-jodatime"
  }
}