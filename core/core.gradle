plugins {
  id 'antlr'
  id 'idea'
}


description = 'Message Format Core'


configurations {
  compile {
    extendsFrom = extendsFrom.findAll { it != configurations.antlr }
  }
}


sourceSets {
  main {
    java.srcDirs = [ "src/main/java", "src/main/generated" ]
  }
  test {
    java.srcDirs = [ "src/test/java", "src/test/generated" ]
  }
}


dependencies {
  implementation "org.antlr:antlr4-runtime:${antlrVersion}"

  antlr "org.antlr:antlr4:${antlrVersion}"
}


generateGrammarSource {
  outputDirectory = file("src/main/generated/de/sayayi/lib/message/parser")
  arguments += [ '-package', 'de.sayayi.lib.message.parser' ]

  exclude "**/MsgParserTest.g4"

  doLast {
    file('src/main/gen').deleteDir()
    file('src/main/antlr/gen').deleteDir()
  }
}


compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}


compileJava.dependsOn generateGrammarSource


idea {
  module {
    resourceDirs += file("src/main/antlr")
  }
}


clean.doLast {
  file('src/main/antlr/gen').deleteDir()
  file('src/main/gen').deleteDir()
  file('src/main/generated').deleteDir()
}
