/*
 * Copyright 2023 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'de.sayayi.plugin.gradle.libapi' version '1.0-SNAPSHOT'
}


description = 'Message Format Library (Spring adopter and formatter)'


def springVersion = '[5.0,6.0)'


dependencies {
  implementation project(':message-format')

  implementation("org.springframework:spring-context:${springVersion}") {
    exclude module: 'spring-jcl'
  }

  testImplementation project(':message-format').sourceSets.test.output
}


def resolvedSpringVersion = configurations
    .compileClasspath
    .resolvedConfiguration
    .resolvedArtifacts
    .find { it.name == 'spring-expression' }
    .moduleVersion.id.version


javadoc {
  (options as StandardJavadocDocletOptions).with {
    linksOffline(
        "https://javadoc.io/doc/de.sayayi.lib/message-format/${rootProject.version}/",
        "${rootDir}/gradle/javadocs/message-format")
    linksOffline(
        "https://javadoc.io/doc/org.springframework/spring-core/${resolvedSpringVersion}/",
        "${rootDir}/gradle/javadocs/spring-core")
    linksOffline(
        "https://javadoc.io/doc/org.springframework/spring-expression/${resolvedSpringVersion}/",
        "${rootDir}/gradle/javadocs/spring-expression")
  }
}


publishing {
  publications {

    spring(MavenPublication) {
      from components.java

      pom {
        name = project.description
        description = 'Highly configurable message format library supporting message definition through annotations'
        url = 'https://github.com/jgremmen/message-format'
        inceptionYear = '2023'

        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/jgremmen/message-format.git'
          developerConnection = 'scm:git:git://github.com/jgremmen/message-format.git'
          url = 'https://github.com/jgremmen/message-format'
        }
      }
    }
  }
}


signing {
  sign publishing.publications.spring
}
