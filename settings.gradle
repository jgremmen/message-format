/*
 * Copyright 2020 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
  repositories {
    // plugin snapshot repository
    maven {
      url = 'https://artifactory.sayayi.de/artifactory/plugins-snapshot/'
      mavenContent {
        includeGroupByRegex 'de\\.sayayi\\.plugin\\.gradle.*'
        snapshotsOnly()
      }
    }

    // default gradle plugin portal
    gradlePluginPortal()
  }
}


buildscript {
  repositories {
    gradlePluginPortal()
    mavenCentral()
  }

  dependencies {
    classpath 'com.adarshr:gradle-test-logger-plugin:4.0.0'
    classpath 'org.danilopianini.publish-on-central:org.danilopianini.publish-on-central.gradle.plugin:9.0.6'
    classpath 'se.bjurr.gitchangelog:git-changelog-gradle-plugin:3.0.6'
  }
}


include 'message-format'
include 'message-format-annotations'
include 'message-format-asm'
include 'message-format-spring'
include 'message-gradle-plugin'

if ('true' == System.getProperty('documentation'))
  include 'documentation'


rootProject.name = 'message-format-projects'
rootProject.children.each { project ->
  project.buildFileName = "${project.name}.gradle"
}
