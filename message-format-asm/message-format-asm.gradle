/*
 * Copyright 2025 Jeroen Gremmen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//file:noinspection DependencyNotationArgument
//file:noinspection GroovyAssignabilityCheck


description = 'Message Format Library (asm adopter)'


dependencies {
  implementation project(':message-format-annotations')
  implementation libs.asm

  compileOnly project(':message-format')

  //noinspection DependencyNotationArgument
  testImplementation project(':message-format').sourceSets.test.output
}


javadoc {
  (options as StandardJavadocDocletOptions).with {
    group('Adopters', [
        'de.sayayi.lib.message.asm.adopter',
    ])

    linksOffline(
        "https://javadoc.io/doc/de.sayayi.lib/message-format/${rootProject.version}/",
        "${rootDir}/gradle/javadocs/message-format")
    linksOffline(
        "https://javadoc.io/doc/org.jetbrains/annotations/${resolveVersion('annotations')}/",
        "${rootDir}/gradle/javadocs/jetbrains-annotations")
  }
}


publishing {
  publications {

    asm(MavenPublication) {
      from components.java

      pom {
        inceptionYear = '2025'

        developers {
          developer {
            id = 'jgremmen'
            name = 'Jeroen Gremmen'
            email = 'jeroen.gremmen@sayayi.de'
          }
        }
      }
    }
  }
}


signing {
  sign publishing.publications.asm
}
